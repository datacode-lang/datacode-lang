# Khi grammar (simplified)
# Simplified for readability, but ambiguous

## Document

<value-document> = *
                 | *<value>*

<dictionary-document> = *
                      | *<dictionary>*

<list-document> = *
                | *<list>*

## Value

<value> = <block>
        | "|" <block>
        | <tuple>
        | "|" <tuple>
        | <tagged-value>

<block> = <term>
        | <term> <block>
        | "~"
        | "~" <block>

<term> = <text>
       | <bracketed-value>
       | <bracketed-dictionary>
       | <bracketed-list>
       | <tagged-arguments>

<text> = <string>
       | <string> <text>
<text'> = <string>
        | <string> <text'>
        | "~" <text'>

<tuple> = <block> "|" <block>
        | <block> "|" <tuple>

<tagged-value> = <tag>":"_<value>

<bracketed-value> = "{" <value> "}"

## Dictionary

<dictionary> = <delimited-dictionary>
             | <aligned-dictionary>

<delimited-dictionary> = <entry>
                       | <entry> ";"
                       | <entry> ";" <delimited-dictionary>

<aligned-dictionary> = <entry>
                     | <entry> <aligned-dictionary>

<entry> = <string>":" <value>

<bracketed-dictionary> = "{" "}"
                       | "{" <dictionary> "}"

## List

<list> = <delimited-list>
       | <aligned-list>
       | <tabular-list>

<delimited-list> = <value>
                 | <value> ";"
                 | <value> ";" <delimited-list>

<aligned-list> = ">" <value>
               | ">" <value> <aligned-list>
               | <tagged-value>
               | <tagged-value> <aligned-list>

<tabular-list> = "|" <block> "|"
               | "|" <block> "|" <tabular-list>
               | "|" <tuple> "|"
               | "|" <tuple> "|" <tabular-list>

<bracketed-list> = "[" "]"
                 | "[" <list> "]"

## Tagged arguments

<tagged-arguments> = <tag>
                   | <tag><arguments>

<arguments> = ":"<simple-argument>
            | ":"<simple-argument><arguments>
            | ":"<tagged-arguments>

<simple-argument> = <string>
                  | <bracketed-value>
                  | <bracketed-dictionary>
                  | <bracketed-list>

## Tag

<tag> = "<"<word>">"
      | "<"<word> <attributes> ">"
      | "<>"

<attributes> = <word>
             | <word> <attributes>
             | <word>":"<string>
             | <word>":"<string> <attributes>

## String

<string> = word
         | transcription
         | text-block
