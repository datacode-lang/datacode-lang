# Math equations
# Document type: Value

<documentclass>:article

<usepackage>:amsmath

# Use brackets around jot to prevent it from taking "1em" as argument.
<setlength>:{<jot>}:1em # Controls math line spacing

<begin>:document

A document containing some
equations and matrices.

<section>:Equations

# The def! macro makes it easier to use LaTeX \newcommand.
<def!>:Log:0:{ <operatorname>:Log }

<begin>:equation* <begin>:split

  <sqrt>:5 <times> <sqrt>:5 = 5
  <n>
  # A trailing apostrophe indicates that the first argument is optional.
  <sqrt'>:3:4 <times> <sqrt'>:3:16 = 4

<end>:split <end>:equation*

<begin>:equation* <Log>(1 + 2 + 3) = <Log>:1 + <Log>:2 + <Log>:3 <end>:equation*

<begin>:align* [
  > 0 + 1 + 2 + <dots> + 99 + 100
  > (0 + 100) + (1 + 99) + <dots> + (49 + 51) + 50
  > 5050
] <end>:align*

<begin>:align* [
  = 0 + 1 + 2 + <dots> + (n - 1) + n;
  = n <cfrac>:n:2 + <cfrac>:n:2;
  = <cfrac>:n^2:2 + <cfrac>:n:2;
  = n <cdot> <cfrac>:{n + 1}:2;
] <end>:align*

<section>:Matrices

<begin>:math
  <mathbf>:X = <begin>:bmatrix [
    |1|0|0|
    |0|1|0|
    |0|0|1|
  ] <end>:bmatrix
  =
  # ~ denotes an empty (nil) value.
  <begin>:bmatrix [1|~|~; ~|1|~; ~|~|1] <end>:bmatrix
<end>:math

<end>:document
