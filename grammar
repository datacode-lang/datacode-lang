# Khi grammar

## Document

<expression-document> = *
                      | *<value>*

<dictionary-document> = *
                      | *<dictionary>*

<table-document> = *
                 | *<table>*

## Value

<value> = <nested-expression>
        | <tuple-expression>
        | <expression>

## Expression

<nested-expression> = <tag>":"_<value>
                    | "<>"":"_<value>

<tuple-expression> = "&" <tuple-expression'>
                   | <expression> "&" <tuple-expression'>
<tuple-expression'> = <expression>
                    | <expression> "&" <tuple-expression'>

<expression> = <word>
             | <word> <expression>
             | <transcription>
             | <transcription> <expression>
             | <text-block>
             | <text-block> <expression>
             | <bracketed-value>
             | <bracketed-value> <expression>
             | <bracketed-dictionary>
             | <bracketed-dictionary> <expression>
             | <bracketed-table>
             | <bracketed-table> <expression>
             | <tuple>
             | <tuple> <expression>
             | <tagged-value>
             | <tagged-value> <expression>
             | "~"
             | "~" <expression>

## Dictionary

<dictionary> = <flow-dictionary>
             | <bullet-dictionary>

<flow-dictionary> = <dictionary-entry>
                  | <dictionary-entry> ";"
                  | <dictionary-entry> ";" <flow-dictionary>

<bullet-dictionary> = ">" <dictionary-entry>
                    | ">" <dictionary-entry> <bullet-dictionary>

<dictionary-entry> = <word> ":" <value>
                   | <transcription> ":" <value>
                   | <text-block> ":" <value>

## Table

### All rows must have the same number of columns.
<table> = <flow-table>
        | <grid-table>
        | <bullet-table>

<flow-table> = <value>
             | <value> ";"
             | <value> ";" <flow-table>
             | <value> "|" <flow-table>

### Cannot have zero columns.
<grid-table> = "|" <value> <grid-table>
             | "|" <grid-table>
             | "|"

<bullet-table> = ">" <value>
               | ">" <value> <bullet-table'>
<bullet-table'> = <bullet-table>
                | "|" <value>
                | "|" <value> <bullet-table'>

## Tuple

<tuple> = "<>"
        | "<>"<arguments>

## Tag & tagged value

### Name cannot start with a hash sign.
<tag> = "<"<word>">"
      | "<"<word> <attributes> ">"

<attributes> = <word>
             | <word> <attributes>
             | <word>":" <word>
             | <word>":" <word> <attributes>
             | <word>":" <transcription>
             | <word>":" <transcription> <attributes>
             | <word>":" <text-block>
             | <word>":" <text-block> <attributes>

<tagged-value> = <tag>
               | <tag><arguments>

## Arguments

<arguments> = ":"<word>
            | ":"<word><arguments>
            | ":"<transcription>
            | ":"<transcription><arguments>
            | ":"<text-block>
            | ":"<text-block><arguments>
            | ":"<bracketed-value>
            | ":"<bracketed-value><arguments>
            | ":"<bracketed-dictionary>
            | ":"<bracketed-dictionary><arguments>
            | ":"<bracketed-table>
            | ":"<bracketed-table><arguments>
            | ":""<>"
            | ":""<>"<arguments>
            | ":"<tag>
            | ":"<tag><arguments>
            | ":""&"":"<tuple>
            | ":""&"":"<tagged-value>

## Bracketed structures

<bracketed-value> = "{" <value> "}"

<bracketed-dictionary> = "{" "}"
                       | "{" <dictionary> "}"

<bracketed-table> = "[" "]"
                  | "[" <table> "]"

## Text & whitespace

### Matches a Word token.
<word>

### Matches a Transcription token.
<transcription>

### Matches a Text block token.
<text-block>

### Any whitespace
### Any number (including zero) of whitespace tokens.
### Also represented by a space character.
*
