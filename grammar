# Khi grammar

## Document

<value-document> → *
                 | *<value>*

<dictionary-document> → *
                      | *<dictionary>*

<list-document> → *
                | *<list>*

## Value

<value> → <inner-value>
        | "|" <inner-value>
        | <tagged-value>

<inner-value> → <block>
              | <tuple>

<tuple> → <block> "|" <block>
        | <block> "|" <tuple>

<tagged-value> → <tag>":"_<value>

<block> → <term>
        | <term> <block>
        | "~"
        | "~" <block>

<term> → <text>
       | <bracketed-value>
       | <bracketed-dictionary>
       | <bracketed-list>
       | <tagged-arguments>

<text>  → <string>
        | <string> <text'>
<text'> → <string>
        | <string> <text'>
        | "~" <text'>

<bracketed-value> → "{" <value> "}"

## Dictionary

<dictionary> → <delimited-dictionary>
             | <aligned-dictionary>
             | <absolute-dictionary>

<delimited-dictionary> → <entry>
                       | <entry> ";"
                       | <entry> ";" <delimited-dictionary>

<aligned-dictionary> → <entry>
                     | <entry>_<aligned-dictionary>

<absolute-dictionary>  → <absolute-dictionary'>
                       | <inner-dictionary>_<absolute-dictionary'>
<absolute-dictionary'> → <section>
                       | <section>_<absolute-dictionary>

<section> → <square-header>":"
          | <square-header>":"_<list>
          | <curly-header>":"
          | <curly-header>":"_<inner-dictionary>
          | <curly-header>":"_<value>

<curly-header> → "{"<key>"}"

<square-header> → "["<key>"]"

<inner-dictionary> → <delimited-dictionary>
                   | <aligned-dictionary>

<entry> → <key>":" <value>

<key> → <string>
      | <string>":"<key>

<bracketed-dictionary> → "{" "}"
                       | "{" <dictionary> "}"

## List

<list> → <delimited-list>
       | <aligned-list>
       | <tabular-list>
       | <tagged-list>

<delimited-list> → <value>
                 | <value> ";"
                 | <value> ";" <delimited-list>

<aligned-list> → ">"_<value>
               | ">"_<value>_<aligned-list>

<tabular-list> → "|" <inner-value> "|"
               | "|" <inner-value> "|"_<tabular-list>

<tagged-list> → <tagged-value>
              | <tagged-value>_<tagged-list>

<bracketed-list> → "[" "]"
                 | "[" <list> "]"

## Tagged arguments

<tagged-arguments> → <tag>
                   | <tag><arguments>

<arguments> → ":"<argument>
            | ":"<argument><arguments>

<argument> → <string>
           | <bracketed-value>
           | <bracketed-dictionary>
           | <bracketed-list>
           | <tagged-arguments>

## Tag

<tag> → "<"<word>">"
      | "<"<word>_<attributes> ">"
      | "<"">"

<attributes> → <attribute>
             | <attribute>_<attributes>

<attribute> → <word>
            | <word>":"<string>

## String

<string> → <word>
         | <transcription>
         | <text-block>
